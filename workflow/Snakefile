import numpy as np

hyperparam_grid = np.linspace(0.01, 2, 20).round(2)


rule all:
    input:
        expand(("results/heatmap_{method}.pdf"), method=["mle", "lse"]),


rule true_biadj:
    output:
        biadj="results/true_udgs/biadj_{idx}.csv",
    conda:
        "envs/basic_ml.yaml"
    script:
        "scripts/true_biadj.py"


rule true_params:
    input:
        biadj=rules.true_biadj.output,
    output:
        weights="results/true_params/graph={idx}_seed={seed}_weights.csv",  # change to json file with all three
        errors="results/true_params/graph={idx}_seed={seed}_errors.csv",
        means="results/true_params/graph={idx}_seed={seed}_means.csv",
    conda:
        "envs/medil.yaml"
    script:
        "scripts/true_params.py"


rule generate_data:
    input:
        weights=rules.true_params.output.weights,  # change to json file with all three
        means=rules.true_params.output.means,
        errors=rules.true_params.output.errors,
    output:
        dataset="results/datasets/graph={idx}/seed={seed}/n={n}_dataset.csv",
    conda:
        "envs/medil.yaml"
    script:
        "scripts/generate_data.py"


rule fit_and_eval_ncfa:
    input:
        dataset=rules.generate_data.output,
        true_biadj=rules.true_biadj.output,
        true_params=rules.true_params.output,
    output:
        weights="results/est_params/method={method}/graph={idx}_seed={seed}_n={n}/lambda={llambda}_mu={mu}_weights.csv",  # change to json file with all three
        errors="results/est_params/method={method}/graph={idx}_seed={seed}_n={n}/lambda={llambda}_mu={mu}_errors.csv",
        means="results/est_params/method={method}/graph={idx}_seed={seed}_n={n}/lambda={llambda}_mu={mu}_means.csv",
        eval="results/eval/method={method}/graph={idx}_seed={seed}_n={n}/lambda={llambda}_mu={mu}_eval.csv",
    conda:
        "envs/medil.yaml"
    script:
        "scripts/fit_ncfa.py"


rule cross_val_csv:
    input:
        expand(
            (rules.fit_and_eval_ncfa.output.eval),
            method=["mle", "lse"],
            idx=range(7),
            seed=[3],
            n=[5000],
            llambda=hyperparam_grid,
            mu=hyperparam_grid,
        ),
    output:
        "results/cross_val.csv",
    conda:
        "envs/plot.yaml"
    script:
        "scripts/join_csv.py"


rule heatmap:
    input:
        rules.cross_val_csv.output,
    output:
        mle="results/heatmap_mle.pdf",
        lse="results/heatmap_lse.pdf",
    conda:
        "envs/plot.yaml"
    script:
        "scripts/heatmap.py"
